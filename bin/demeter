#!/usr/bin/perl

use strict;
use warnings;
use File::Basename qw/dirname/;
use lib dirname(__FILE__) . '/../lib';
use DataDownloader::Util qw(make_logger);
use MooseX::FollowPBP;
use Getopt::Long;
use YAML;
use Carp;
use Module::Find qw/findsubmod/;

my @available_sources = grep {$_ !~ /ABC$/} findsubmod( 'DataDownloader::Source' );

my $config = "testing";
my $verbose = 0;
my $show_sources = 0;
my $show_help = 0;
my $confdir = $ENV{HOME} . '/.demeter';

my $script = __FILE__;

my $result = GetOptions(
    "environment=s" => \$config,
    "confdir=s"     => \$confdir,
    "verbose"       => \$verbose,
    "sources"       => \$show_sources,
    "help"          => \$show_help,
);

if ($show_help) {
    warn <<"HELP";

$script
Download data from specific sources

Usage: perl $script -e environment Source1 Source2...

Options:
  -e --environment  The set of configuration options to load. Defaults to "testing"
  -s --sources      Show the list of available sources
  -h --help         Show this help
  -c --confdir      An optional configuration directory. Defaults to ~/.demeter

If no sources are selected, then then the default list from the 
configuration file will be loaded and run.

If the enviroment value points to a file, that will be loaded. If it names
an environment, then a file named \$env.yml will be looked for first in
\$confdir (if supplied), and then in the config directory of this distribution.

HELP
    exit;
}

if ($show_sources) {
    warn "Available sources:\n";
    for my $src (sort @available_sources) {
        $src =~ s/.*:://;
        warn "\t- $src\n";
    }
    exit;
}

# RESOLVE THE CONFIG FILE TO USE.
my $config_file;
if ($config =~ m[/]) {
    # Assume this is a qualified path.
    $config_file = $config;
} else {
    for my $dir ($confdir, dirname(__FILE__) . '/../config') {
        $config_file = "$dir/$config.yml";
        last if (-e $config_file);
    }
}
# Check it exists.

unless (-e $config_file) {
    croak <<"USAGE";

Cannot find resolve a config file for the $config environment.

Usage: $script -e env Uniprot

USAGE
}

my ($LOGDIR, $DATA_DIR, $DEFAULT_SOURCES, $OPTS) = read_config($config_file);

my $logger =  make_logger($LOGDIR);
my %args   = (data_dir => $DATA_DIR, logger => $logger);
my @sources_to_get = (@ARGV) ? @ARGV : @$DEFAULT_SOURCES;

for my $source (@sources_to_get) {
    my $package = "DataDownloader::Source::$source";
    eval "require $package";
    if (my $e = $@) {
        $logger->error("Failed to load source $source:", $e);
        next;
    }
    my $source_options = $OPTS->{$source} || {};
    eval {
        my $downloader = $package->new(%args, options => $source_options);
        $downloader->get_data();
    };
    if (my $e = $@) {
        $logger->error("Failed to download source $source:", $e);
    }
} 

exit;

##### FUNCTIONS ####

sub read_config {
    my $config_file = shift;

    my $settings = YAML::LoadFile($config_file);

    my ($log_dir, $data_dir) = @{$settings}{qw/log_dir data_dir/};
    ($log_dir && $data_dir) or croak <<"USAGE";

The config file must supply a value for both log_dir and data_dir.

Usage: $script -e env Source
USAGE

    my $LOGDIR = ($settings->{log_dir} =~ m{^/}) 
        ? $settings->{log_dir}
        : $ENV{HOME} . '/' . $settings->{log_dir};
    my $DATA_DIR = ($settings->{log_dir} =~ m{^/})
        ? $settings->{data_dir}
        : $ENV{HOME} . '/' . $settings->{data_dir};

    my $DEFAULT_SOURCES = $settings->{default_sources} || [];

    return ($LOGDIR, $DATA_DIR, $DEFAULT_SOURCES, $settings);
}
